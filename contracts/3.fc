{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}

() recv_internal() {
}

int can_store_bits?(builder b, int bits) asm "BCHKBITSQ";

(slice, slice) load_bit(slice cs) asm( -> 1 0) "1 LDSLICE";

((tuple, builder), ()) ~store((tuple, builder) rs, slice v) inline_ref {
  var (builders, res) = rs;
  ifnot res.can_store_bits?(v.slice_bits()) {
    builders = cons(res, builders);
    res = begin_cell();
  }
  res = res.store_slice(v);
  return ((builders, res), ());
}

((tuple, builder), ()) ~store_b((tuple, builder) rs, builder b) inline_ref {
  var (builders, res) = rs;
  ifnot res.can_store_bits?(b.builder_bits()) {
    builders = cons(res, builders);
    res = begin_cell();
  }
  res = res.store_builder(b);
  return ((builders, res), ());
}

int ubitsize(int x) asm "UBITSIZE"; 

builder solve(int flag, slice value, slice text) {
  int flen = ubitsize(flag);

  tuple builders = null();
  builder res = begin_cell();

  while (flen <= text.slice_bits()) {
    (slice text', int x) = text.load_uint(flen);
    
    slice v = value;
    if (x == flag) {
      text = text';
    } else {
      v = text~load_bit();
    }
    ;; (builders, res)~store(v); -- replaced for efficency
    ifnot res.can_store_bits?(v.slice_bits()) {
      builders = cons(res, builders);
      res = begin_cell();
    }
    res = res.store_slice(v);
  } 

  ifnot (text.slice_refs()) {
    (builders, res)~store(text);
  } else {
    slice next = text.preload_ref().begin_parse();
    int tlen = text.slice_bits();
    int nlen = next.slice_bits();
    int ll = max(1, flen - nlen);
    while tlen >= ll {
      int cnt = flen - tlen;
      int xu = text.preload_uint(tlen);
      (slice next', int xl) = next.load_uint(cnt);
      int x = (xu << cnt) + xl;

      slice v = value;
      if (x == flag) {
        next = next';
        tlen = 0;
      } else {
        v = text~load_bit();
        tlen -= 1;
      }
      (builders, res)~store(v);
    }
    if (tlen > 0) {
      (builders, res)~store(text);
    }

    builder res' = solve(flag, value, next);
    (builders, res)~store_b(res');
  }

  while ~ builders.null?() {
    builder res' = builders~list_next();
    res = res'.store_ref(res.end_cell());
  }

  return res;
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  slice value' = begin_cell().store_uint(value, ubitsize(value)).end_cell().begin_parse();

  builder res = solve(flag, value', linked_list.begin_parse());

  return res.end_cell();
}

