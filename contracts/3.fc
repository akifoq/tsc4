{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}

() recv_internal() {
}

int can_store_bits?(builder b, int bits) asm "BCHKBITSQ";

int ubitsize(int x) asm "UBITSIZE"; 

global slice value;

((tuple, builder), ()) ~store((tuple, builder) br, slice v) asm """
  // builders res v
  STSLICERQ // .. res v -1 or res' 0
  IF:<{ 
    s0 s2 XCHG CONS	// v blds
    SWAP NEWC // .. blds v res
    STSLICE // blds res
  }>
  // builders res
""";

builder solve(int flag, slice text) {
  slice next = null();
  if (text.slice_refs()) {
    next = text~load_ref().begin_parse();
  }
  flag~impure_touch();
  text~impure_touch();

  int flen = ubitsize(flag);  

  tuple builders = null();
  builder res = begin_cell();

  do {
    while (flen <= text.slice_bits()) {
      (slice text', int x) = text.load_uint(flen);
      
      slice v = null();
      if (x == flag) {
        v = value;
        text = text';
      } else {
        v = text~load_bits(1);
      }
      
      (builders, res)~store(v); 
    } 

    ifnot next.null?() {
      int nlen = next.slice_bits();
      int plen = min(nlen, 1023 - text.slice_bits());
      slice pref = next~load_bits(plen);
      builder b = begin_cell().store_slice(text).store_slice(pref);
      text = b.end_cell().begin_parse();

      if next.slice_data_empty?() {
        if next.slice_refs() {
          next = next.preload_ref().begin_parse();
        } else {
          next = null();
        }
      }
    } else {
      (builders, res)~store(text);
      text = next;
    }
  } until text.null?();

  while ~ builders.null?() {
    builder res' = builders~list_next();
    res = res'.store_ref(res.end_cell());
  }

  return res;
}

;; testable
(cell) find_and_replace(int flag, int _value, cell linked_list) method_id {
  value = begin_cell().store_uint(_value, ubitsize(_value)).end_cell().begin_parse();

  builder res = solve(flag, linked_list.begin_parse());

  return res.end_cell();
}

