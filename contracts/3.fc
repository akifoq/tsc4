{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}

() recv_internal() {
}

int can_store_bits?(builder b, int bits) asm "BCHKBITSQ";

(int, slice) read_bits(slice text, int flen) {
  int to_read = min(text.slice_bits(), flen);
  int value = text~load_uint(to_read);

  flen -= to_read;
  if (flen == 0) { 
    return (value, text);
  }

  if (text.slice_refs()) {
    (int rest, slice text') = read_bits(text.preload_ref().begin_parse(), flen);
    return ((value << flen) + rest, text');
  }

  return (0, null());
}

builder solve(int flag, int flen, slice value, slice text) {
  while text.slice_data_empty?() {
    if text.slice_refs_empty?() {
      return begin_cell();
    }
    text = text.preload_ref().begin_parse();
  }

  (int x, slice text') = read_bits(text, flen);
  builder res = null();
  if (~ text'.null?()) & (x == flag) {
    res = begin_cell().store_slice(value);
    text = text';
  } else {
    int bit = text~load_uint(1);
    res = begin_cell().store_uint(bit, 1);
  }

  builder res' = solve(flag, flen, value, text);
  if (res.can_store_bits?(res'.builder_bits())) {
    res = res.store_builder(res');
  } else {
    res = res.store_ref(res'.end_cell());
  }
  return res;
}

int ubitsize(int x) asm "UBITSIZE";

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  slice value' = begin_cell().store_uint(value, ubitsize(value)).end_cell().begin_parse();

  builder res = solve(flag, ubitsize(flag), value', linked_list.begin_parse());

  return res.end_cell();
}

