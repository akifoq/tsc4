{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}


cell empty_cell() asm "<b b> PUSHREF";

;; testable
(cell) find_branch_by_hash(int hash, cell cur) method_id {
    if (cell_hash(cur) == hash) {
        return cur;
    }
    
    tuple t = cons(cur, null());
    do {
        cell c = t~list_next();
        slice cs = c.begin_parse();
        repeat (cs.slice_refs()) {
            cell ref = cs~load_ref();
            if (cell_hash(ref) == hash) {
                return ref;
            }
            t = cons(ref, t);
        }
    } until t.null?();
    return empty_cell();
}
