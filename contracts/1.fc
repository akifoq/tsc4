{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}


cell empty_cell() asm "<b b> PUSHREF";

((tuple, tuple), ()) ~push_back((tuple, tuple) q, cell c) inline {
    var (head, tail) = q;
    tail = cons(c, tail);
    return ((head, tail), ());
}

((tuple, tuple), cell) ~pop_front((tuple, tuple) q) inline {
    var (head, tail) = q;
    if head.null?() {
        do {
            cell c = tail~list_next();
            head = cons(c, head);
        } until tail.null?();
    } 
    cell c = head~list_next();
    return ((head, tail), c);
}

int queue_empty?((tuple, tuple) q) inline {
    var (head, tail) = q;
    return tail.null?() & head.null?();
}

;; testable
(cell) find_branch_by_hash(int hash, cell cur) method_id {
    (tuple, tuple) q = (null(), null());
    q~push_back(cur);
    do {
        cell c = q~pop_front();
        if (hash == cell_hash(c)) {
            return c;
        } else {
            slice cs = c.begin_parse();
            repeat (cs.slice_refs()) {
                q~push_back(cs~load_ref());
            }
        }
    } until q.queue_empty?();
    return empty_cell();
}
